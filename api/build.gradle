plugins {
	id 'java-library'
	id "io.freefair.lombok" version "${ioFreeFairLombok}"
	id 'maven-publish'
	id "jacoco"
}

repositories {
	mavenCentral()
}

group = 'fi.poltsi.vempain'
version = project.hasProperty("releaseVersion") ? project.releaseVersion : '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(javaVersion.toInteger())
	}
}

jar {
	archiveFileName = "vempain-auth-api-${version}.jar"
}

dependencies {
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-annotations
	api "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
	api "org.springframework.boot:spring-boot-starter-webmvc:${springBootVersion}"
	implementation "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
	implementation "org.springframework.boot:spring-boot-starter-validation:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-jackson:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-security-test:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-webmvc-test:${springBootVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-data-jpa-test:${springBootVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	testImplementation "org.junit.platform:junit-platform-launcher:${junitVersion}"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifactId = 'vempain-auth-api'
		}
	}
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/Vempain/vempain-auth")
			credentials {
				username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") ?: ""
				password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token") ?: ""
			}
		}
	}
}

jacoco {
	toolVersion = "0.8.13"
}

tasks.named('test') {
	useJUnitPlatform()
}

test {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required = true
	}
}
