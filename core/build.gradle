plugins {
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version "${springBootVersion}"
	// https://mvnrepository.com/artifact/io.freefair.lombok/io.freefair.lombok.gradle.plugin
    id "io.freefair.lombok" version "${ioFreeFairLombok}"
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'fi.poltsi.vempain'
version = project.hasProperty("releaseVersion") ? project.releaseVersion : '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

jar {
	archiveFileName = "vempain-auth-core-${version}.jar"
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok'
    implementation project(':api')
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: "${springBootVersion}"
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "${springBootVersion}"
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: "${jjwtVersion}"
	implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: "${swaggerVersion}"
	// https://mvnrepository.com/artifact/jakarta.annotation/jakarta.annotation-api
	implementation group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '3.0.0'
	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.7'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "${springBootVersion}"
	testImplementation platform("org.junit:junit-bom:${junitVersion}")
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: "${junitVersion}"
	testImplementation group: 'org.testcontainers', name: 'junit-jupiter', version: "${testContainersVersion}"
	testImplementation group: 'org.testcontainers', name: 'postgresql', version: "${testContainersVersion}"
	testImplementation group: 'org.flywaydb', name: 'flyway-core', version: "${flywayVersion}"
    // https://mvnrepository.com/artifact/org.springframework.security/spring-security-test
    testImplementation group: 'org.springframework.security', name: 'spring-security-test', version: '6.5.3'
	// https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.19.0'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: "${jjwtVersion}"
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: "${jjwtVersion}"
	runtimeOnly group: 'org.flywaydb', name: 'flyway-database-postgresql', version: "${flywayVersion}"
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
			artifactId = 'vempain-auth-core'
        }
    }
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/Vempain/vempain-auth")
			credentials {
				username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") ?: ""
				password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token") ?: ""
			}
		}
	}
}
