plugins {
	id 'java-library'
	id 'maven-publish'
	id "io.freefair.lombok" version "${ioFreeFairLombok}"
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'fi.poltsi.vempain'
version = project.hasProperty("releaseVersion") ? project.releaseVersion : '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(javaVersion.toInteger())
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

jar {
	archiveFileName = "vempain-auth-core-${version}.jar"
}

dependencies {
	annotationProcessor 'org.projectlombok:lombok'
	implementation project(':api')
	implementation "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
	implementation "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
	implementation "io.jsonwebtoken:jjwt-api:${jjwtVersion}"
	implementation "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
	implementation "tools.jackson.datatype:jackson-datatype-jsr310:3.0.0-rc2"
	// https://mvnrepository.com/artifact/jakarta.annotation/jakarta.annotation-api
	implementation "jakarta.annotation:jakarta.annotation-api:3.0.0"
	// https://mvnrepository.com/artifact/org.postgresql/postgresql
	implementation "org.postgresql:postgresql:42.7.8"
	// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	implementation "org.apache.commons:commons-lang3:3.20.0"
	implementation "org.springframework:spring-jdbc:6.2.14"
	testImplementation platform("org.junit:junit-bom:${junitVersion}")
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	testImplementation "org.junit.platform:junit-platform-launcher:${junitVersion}"
	testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
	testImplementation "org.testcontainers:testcontainers-junit-jupiter:${testContainersVersion}"
	testImplementation "org.testcontainers:testcontainers-postgresql:${testContainersVersion}"
	testImplementation "org.flywaydb:flyway-core:${flywayVersion}"
	// https://mvnrepository.com/artifact/org.springframework.security/spring-security-test
	testImplementation "org.springframework.security:spring-security-test:6.5.6"
	// https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
	testImplementation "org.mockito:mockito-junit-jupiter:5.20.0"
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"
	runtimeOnly "org.flywaydb:flyway-database-postgresql:${flywayVersion}"
	// Ensure the JUnit Platform launcher is on the runtime classpath (required by Gradle 8/9)
}

test {
	useJUnitPlatform()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifactId = 'vempain-auth-core'
		}
	}
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/Vempain/vempain-auth")
			credentials {
				username = System.getenv("GITHUB_ACTOR") ?: project.findProperty("gpr.user") ?: ""
				password = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.token") ?: ""
			}
		}
	}
}
